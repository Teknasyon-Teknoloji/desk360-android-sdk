apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven-publish'
def sdkArtifactId = 'desk360-android-sdk'
def sdkGroupId = 'com.github.Teknasyon-Teknoloji'
def sdkVersionName = "1.0.9"

android {
    namespace 'com.teknasyon.desk360'

    compileSdk 33

    defaultConfig {
        minSdk 16
        targetSdk 33
        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "VERSION_NAME", "\"$sdkVersionName\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dokka {
        outputFormat = 'gfm'
        outputDirectory = "$buildDir/javadoc"
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.multidex:multidex:2.0.1'

    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'

    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api "androidx.lifecycle:lifecycle-common-java8:2.6.2"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //navigation controller
    api "androidx.navigation:navigation-fragment-ktx:$nav_version"
    api "androidx.navigation:navigation-ui-ktx:$nav_version"

    //http connection libs
    api 'com.google.code.gson:gson:2.9.0'
    api "com.squareup.retrofit2:retrofit:2.9.0"
    api "com.squareup.retrofit2:converter-gson:2.9.0"
    api "com.squareup.okhttp3:logging-interceptor:4.11.0"
    api "com.squareup.retrofit2:converter-scalars:2.6.2"

    //recyclerview
    api 'androidx.recyclerview:recyclerview:1.3.1'
    api 'androidx.core:core-ktx:1.10.1'

    //
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxjava:2.2.17'

    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.9.0'
    api 'com.karumi:dexter:6.2.3'

    api 'com.github.bumptech.glide:glide:4.15.1'
    kapt 'com.github.bumptech.glide:compiler:4.14.2'
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                from components.release
                groupId sdkGroupId
                artifactId sdkArtifactId
                version sdkVersionName
                artifact(sourceJar)
            }
        }
    }
}
